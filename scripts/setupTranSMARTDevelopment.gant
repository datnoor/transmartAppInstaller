import org.apache.tools.ant.taskdefs.Ant

import java.util.concurrent.TimeUnit

/*************************************************************************
 * transmartAppInstaller for tranSMART - translational medicine data mart
 *
 *
 * Apache License, Version 2.0, January 2004
 *
 * Copyright 2012 The Hyve B.V.
 *
 * Author : pieter lukasse (pieter@thehyve.nl)
 *
 *
 * NOTE: This installer is for GPL1.1_Beta_RC4
 *
 ******************************************************************/



/**
 * This script automates the steps needed to setup:
 *
 * the tranSMART DB
 * the i2b2 DB
 * demo data
 *
 * It does this in a Postgres DB. This setup is well suited for a development environment.
 *
 * Pre-requisites (available from command line - or configured in setupTranSMARTDevelopment.properties):
 *
 *  - ant
 *  - psql (PostgreSQL)
 *  - a properly configured setupTranSMARTDevelopment.properties
 *
 * @author : pieter lukasse (pieter@thehyve.nl)
 */

info = this.&echo.curry('info')
warn = this.&echo.curry('warning')

def properties = readProperties("setupTranSMARTDevelopment.properties")
properties.getAt = { delegate.getProperty(it) }

String.metaClass.or = { String s -> new File(delegate, s).path }
String.metaClass.compareVersion = { String s ->
    def arrs = [delegate, s].collect { it.split(/\./) as List }
    while (arrs.any()) {
        def cur = arrs*.getAt(0).collect { it ?: 0 } as int[]
        def r = cur[0] <=> cur[1]
        if (r) return r
        arrs = arrs*.drop(1)
    }
    return 0
}
File.metaClass.or = { String s -> new File(delegate, s).path }

tools_psql_cmd                   = properties["tools.psql.cmd"]
tools_ant_cmd                    = properties["tools.ant.cmd"]

ports_solr                       = properties["ports.solr"]

locations_tomcat                 = properties["locations.tomcat"]
locations_plink_exec             = properties["locations.plink.executables"]

build_dir                        = properties["locations.build.out"]
transmart_db_name                = "transmart"  //untested with other names
transmart_db_tablespace_location = properties["locations.db.tablespaces"]
transmartuser_home               = properties["locations.transmartuser.home"]

pg_user                          = properties['postgres.user'] // database user
pg_password                      = properties['postgres.password']
pg_database                      = properties['postgres.workingdb']
pg_host                          = properties['postgres.host']
pg_port                          = properties['postgres.port']

webapps_subdir                   = 'applications'
webapps_data                     = 'appdata'

version_tag                      = properties['github.version.tag']

//some handy derived globals:
ant_last_step_output             = new File(getTempDir(), "ant_last_step_out.txt")

/*  Set in validate: */
host_os_type = null
exec_postfix = null
psql_sudo_user = null
pg_effective_user = null
pg_process_user = null

validateEnvironment()

def validateEnvironment() {
    def result

    /* System idiosyncrasies */
    if (System.properties['os.name'].toLowerCase().contains('windows'))
        host_os_type = "windows"
    else
        host_os_type = "unix"

    exec_postfix = host_os_type == "windows" ? '.exe' : ''

    /* {{{ psql */
    tools_psql_cmd = tools_psql_cmd ?: which('psql')
    if (!tools_psql_cmd) {
        fail 'psql executable location not specified'
    }
    if (!new File(tools_psql_cmd).canExecute()) {
        fail 'psql location "%s" is invalid', tools_psql_cmd
    }

    if (!pg_database && !System.getenv('PGDATABASE')) {
        pg_database = pg_user ?: System.getProperty("user.name")
        warn 'postgres.workingdb was not specified, trying "%s"', pg_database
    }

    // Test whether we want to change user
    psql_sudo_user = null
    if (host_os_type == 'unix') {
        result = executePsql(['-t', '-c', 'select 1'], failOnError: false)

        if (!result) {
            if (!pg_user) {
                warn "Not trying sudo because no db user was specified"
                fail "Failed connecting to the database. Check your settings;" +
                        " see README for more information"
            }

            warn "Failed connecting to the database. Trying now with sudo"
            psql_sudo_user = pg_user
            result = executePsql(['-t', '-c', 'select 2'], failOnError: false)

            if (!result) {
                info "Could not connect to the database. Check your settings;" +
                        " see README for more information"
                fail "Abort"
            }
        }
    }
    result = executePsql(['-t', '-c', 'select rolsuper, current_user ' +
            'from pg_authid where rolname = current_user'], failOnError: false)
    if (!result) {
        fail "Could not execute postgresql superuser probe. Check your settings"
    }
    def output = ant_last_step_output.text.split(/\|/)*.trim() as List
    if (output[0] != "t") {
        fail "The user we're connecting as, '%s', is not a DB super user",
                output[1]
    }
    pg_effective_user = output[1]
    info "We are using the database role '%s'", output[1]

    if (host_os_type == 'unix') {
        def q = new java.util.concurrent.ArrayBlockingQueue(1)
        executePsql(['-t', '-q'],
                callback: { Map params, List command ->
                    Thread.start {
                        def pb = new ProcessBuilder(*command)
                        pb.environment().putAll(params.environment)
                        def p = pb.start()
                        p.out << 'select pg_backend_pid();\n'
                        def r = new BufferedReader(new InputStreamReader(p.in))
                        def pid = r.readLine()?.trim()
                        if (pid)
                            q.add("ps -ouser= -p $pid".execute().in.text.trim())
                        else
                            q.add(false)
                        p.in.close()
                    }
                });

        pg_process_user = q.poll(3, TimeUnit.SECONDS)
        if (!pg_process_user) {
            warn "Could not determine the user PostgreSQL runs as"
        } else {
            info "PostgreSQL user detected: '%s'", pg_process_user
        }
    }
    /* }}} */

    /* {{{ Ant */
    if (!tools_ant_cmd) {
        if (host_os_type == 'unix') {
            def thisFile = binding.'gant.file'.substring('file:'.length())
            tools_ant_cmd = new File(thisFile).parent | 'ant'
        } else {
            fail 'Ant location not specified. Check tools.ant.cmd'
        }
    }
    if (!new File(tools_ant_cmd).canExecute()) {
        fail 'Ant location "%s" is invalid', tools_ant_cmd
    }
    result = commandStepN([tools_ant_cmd, "-version"], failOnError: false)
    if (!result) {
        fail "Failed on invocation of ant -version"
    }
    /* }}} */

    /* {{{ Validation of directories */
    def dirValidation = { settingName, directory ->
        info "Checking directory %s...", directory
        if (!directory) {
            fail "%s is not set", settingName
        }
        if (!new File(directory).exists()) {
            warn "Directory %s does not exist; trying to create...", directory
            ant.mkdir(dir: directory)
        }
        if (!new File(directory).canWrite()) {
            fail "No write permissions in %s, check %s", directory, settingName
        }
    }
    dirValidation "locations.build.out",          build_dir
    dirValidation "locations.db.tablespace",      transmart_db_tablespace_location
    dirValidation "locations.transmartuser.home", transmartuser_home
    /* }}} */

    info "Everything validated, let's start"
}

target('default': "cleanup and setup") {
    //clean()
    clean_install_scripts()
}

target(cleanBuildDir: 'delete build dir contents')
{
    /* This target must be invoked explicitly */
    delete(verbose: true, includeemptydirs: true) {
        fileset dir: build_dir, includes: '**/*', defaultexcludes: false
    }
}

target(clean: "clean-up to run again...")
{
	clean_db()
	clean_config_files()    
}

target(clean_db: "clean out the database...")
{
    //drop postgres "transmart" db:
    echo ("Kill existing connections to transmart DB, then drop it")
	killTransmartDBConnections();    
    def command =  "drop database if exists $transmart_db_name"
    dbCommand(command, failOnError: true)
    
    //drop postgres tablespaces
    tablespaces = [ 'biomart', 'deapp', 'indx', 'search_app', 'transmart' ];
    tablespaces.each {
    	dbCommand("DROP TABLESPACE IF EXISTS $it", failOnerror: false, quiet: true)
    }
    //drop roles
    roles = ['biomart', 'biomart_user', 'deapp', 'i2b2demodata', 'i2b2hive',
            'i2b2metadata', 'i2b2pm', 'i2b2workdata', 'searchapp', 'tm_cz',
            'tm_lz', 'tm_wz', 'pentaho'];
    command = "reassign owned by ${roles.join(', ')} to $pg_effective_user;"
    dbCommand(command, failOnError: false)
    roles.each {
        dbCommand("drop role $it", failOnError: false, quiet: true)
    }
}

target(clean_config_files: "clean out the config files...")
{
    //remove <user_home>/.grails/transmartConfig files:
    delete(dir: transmartuser_home | ".grails/transmartConfig")
}

target(clean_install_scripts: "Cleanup and Install from scripts only the GEO studies, GSE9150, GSE4382, GSE14860") {
	
	def scriptDir = build_dir | "transmart-db/postgresql_wGEO"
	ensureTablespaces()
	
	//Fetch db scripts
	fetchDBScripts()
	
	//Drop/Create transmart schema
    createTransmartDB()
	
	//Update tablespaces
	scriptFileName = "tablespaces.sql"
	updateStudyInstallTablespaceLocations(scriptDir | scriptFileName, "__TABLESPACES_DIR__")

//  tablespaces already runs within create-main so commented out the next line
//	dbScriptStep(scriptFileName, transmart_db_name, true, true, scriptDir);
	
	//Create schemas
	scriptFileName = "create-main.sql"
	dbScriptStep(scriptFileName, transmart_db_name, true, true, scriptDir);
	
	//clean & setup transmart .grails config files
	clean_config_files()
	configTransmartApp()
	
}

target(install_dump: "Install from dump only the GEO studies, GSE9150, GSE4382, GSE14860") {
    
    def scriptDir = build_dir | "transmartApp-DB/postgresql_wGEO_dump"
    
    ensureTablespaces() /* do this first due to high probability of failure */

    createTransmartDB()
   
    fetchDBScripts()    

    addRoles()    

    createTablespaces()

    installGEOStudies()    
        
    configSteps()
    
}

target(fetchDBScripts: "fetch the ${version_tag} tag of transmartApp-DB")
{
	info("Fetching the ${version_tag} tag of transmartApp-DB. This could take a while...")
	ant.get(src: "https://github.com/transmart/transmartApp-DB/archive/${version_tag}.zip", dest: build_dir | "transmartApp-DB-${version_tag}.zip")
	ant.unzip(src: build_dir | "transmartApp-DB-${version_tag}.zip", dest: build_dir)
	ant.move(file: build_dir | "transmartApp-DB-${version_tag}", tofile: build_dir | "transmartApp-DB")
}

target(createTablespaces: "Create the tablespace entries for the GEO studies")
{
    def scriptDir = build_dir | "transmartApp-DB/postgresql_wGEO_dump"
    scriptFileName = "tablespaces.sql"
    updateStudyInstallTablespaceLocations(scriptDir | scriptFileName, "/opt/transmart/db")
    dbScriptStep(scriptFileName, transmart_db_name, true, true, scriptDir);
}

target(addRoles: "Add the database roles for GEO studies")
{
    def scriptDir = build_dir | "transmartApp-DB/postgresql_wGEO_dump"
    def scriptFileName = "roles.sql"
    dbScriptStep(scriptFileName, transmart_db_name, true, true, scriptDir);
}

target(installGEOStudies: "install the three GEO studies that were pulled from the GA1.0 release")
{
	echo ("loading dump files - takes a while - up to 10 min")
	
    def scriptDir = build_dir | "transmartApp-DB/postgresql_wGEO_dump"
    def file = new File(scriptDir, "all_but_tm.dump")
    executePsql(
       dir: build_dir,
       database: transmart_db_name,
       [file.getPath()],
       new File(tools_psql_cmd).parent.toString() | 'pg_restore' + exec_postfix
    )

    file = new File(scriptDir, "tm_schema_only.dump")
    executePsql(
        dir: build_dir,
        database: transmart_db_name,
        [file.getPath()],
        new File(tools_psql_cmd).parent.toString() | 'pg_restore' + exec_postfix
	)
}

def updateStudyInstallTablespaceLocations(String scriptFileLocation, String targetString)
{
    //read script file and replace targetString with transmart_db_tablespace_location
    def scriptTemp = file2String(scriptFileLocation)
    scriptTemp = scriptTemp.replaceAll(targetString, transmart_db_tablespace_location)
    string2File(scriptTemp, scriptFileLocation)
    return scriptFileLocation;
}

target(configSteps: "final configuration steps")
{
    configTransmartApp()
}

target(configTransmartApp: "setting up the configuration files in <user home>/.grails/transmartConfig/")
{
    //git clone https://github.com/transmart/transmartApp-Config.git  branch   postgresMigrate
    def transmartConfigDir = build_dir | "transmartApp-Config"
      ant.get(src: "https://github.com/transmart/transmartApp-Config/archive/GPL1.1_Beta_RC4.zip", dest: build_dir | "transmartApp-Config-${version_tag}.zip")
      ant.unzip(src: build_dir | "transmartApp-Config-${version_tag}.zip", dest: build_dir)
	  ant.move(file: build_dir | "transmartApp-Config-${version_tag}", tofile: build_dir | "transmartApp-Config")

//    Checkout contains:
//    External configuration files for transmart application
//    Config.groovy - transmart application configuration file
//    DataSource.groovy - jdbc and connection pool configuration
//    RmodulesConfig.groovy - configuration file for the R plugin module
//
//    these files should be put under this folder:
//        ${transmartUser HOME Dir}/.grails/transmartConfig

//    TODO NOTE: the name of the configuration directory is determined by the name
//    of the application, and the name of the application is set in the file
//    application.properties in the top level directory of transmartApp. For now, this script is assuming the name "transmart" is set.

    def transmartConfigAtUserHome = transmartuser_home | ".grails/transmartConfig"
    mkdir(dir: transmartConfigAtUserHome)

    //Copy all .groovy files to this folder:
    copy(todir: transmartConfigAtUserHome) {
        fileset (dir: transmartConfigDir) {
            include(name: "*.groovy")
        }
    }
    //nb: the ant as in "ant.copy" above is not needed. Calling Ant tasks in a Gant closure is so common that Gant automatically
    //tries the ant object during function lookup. Hence the ant. prefix is not needed

    //adjust Config.groovy:
    def configGroovy = transmartConfigAtUserHome | "Config.groovy"

    replacePropertyInFile(configGroovy,
            "com.recomdata.searchengine.index",
            locations_tomcat | webapps_data | "transmart/index", true)

    //TODO  genePattern (tool from BroadInstitute) settings?

    //PLINK config: Whole genome association analysis toolset from http://pngu.mgh.harvard.edu/~purcell/plink/
    replacePropertyInFile(configGroovy,
            "com.recomdata.datasetExplorer.plinkExcutable", locations_plink_exec, true)

    //SOLR ("Lucene-based search server" from Apache) config:
    replacePropertyInFile(configGroovy,
            "com.recomdata.solr.baseURL", "http://localhost:${ports_solr}", true)

    //This is the directory to the R plugins.
    replacePropertyInFile(configGroovy,
            "com.recomdata.plugins.pluginScriptDirectory",
            locations_tomcat | webapps_subdir | "transmart/plugins/", true)

    //This is the main temporary directory, under this should be the folders that get created per job.
    replacePropertyInFile(configGroovy,
            "com.recomdata.plugins.tempFolderDirectory",
            getTempDir() | "jobs", true)

    //Use this to do local development.  It causes the analysis controllers to move the image file before rendering it.
    replacePropertyInFile(configGroovy,
            "com.recomdata.plugins.transferImageFile", "true", false)

    //This is the system path where we move the image file to so we can serve it.
    replacePropertyInFile(configGroovy,
            "com.recomdata.plugins.temporaryImageFolder",
            locations_tomcat | webapps_subdir | "transmart/images/tempImages", true)

    //some dir related to R ?
    replacePropertyInFile(configGroovy,
            "com.recomdata.transmart.data.export.rScriptDirectory",
            locations_tomcat | webapps_subdir | "transmart/dataExportRScripts", true)


    //TODO : do something with com.recomdata.plugins.analysisImageURL?


    //TODO - edit DataSource.config to reflect your postgreSQL (you might have to change host, port, username, password)    - this is left with DEFAULT values for now


    //Edit RModulesConfig.groovy, look at these values
    //
    //    pluginScriptDirectory
    //    tempFolderDirectory
    //    temporaryImageFolder (writable)
    //    imageURL (corresponds to temporaryImageFolder) - TODO - check how this can be used...?
    def rmodulesGroovy = transmartConfigAtUserHome | "RModulesConfig.groovy"
    //pluginScriptDirectory, temporaryImageFolder
    replaceInFile(rmodulesGroovy, "/your/tomcat", locations_tomcat)
     //tempFolderDirectory
    replaceInFile(rmodulesGroovy,
            "tempFolderDirectory = \"/var/tmp/jobs/\"",
            "tempFolderDirectory = \"" + getTempDir() +"jobs/\"" )

}

def killTransmartDBConnections() {
	def killconnectionscmd = "SELECT pg_terminate_backend(pid) FROM pg_stat_activity WHERE datname = 'transmart'"    
    dbCommand([:], killconnectionscmd)
}

target(createTransmartDB: "createTransmartDB")
{
	killTransmartDBConnections();
    
    def droptransmartcmd = "DROP DATABASE IF EXISTS transmart"	    
    dbCommand([:], droptransmartcmd)
    
    def command = "CREATE DATABASE $transmart_db_name " +
            "WITH OWNER = $pg_effective_user TEMPLATE template0 " +
            "ENCODING='UTF-8' LC_COLLATE='en_US.UTF-8'"

    dbCommand([:], command)
}

target(ensureTablespaces: "create tablespace directories")
{
    def scriptUser = System.getProperty("user.name")
    def attemptChown = host_os_type == 'unix' && scriptUser == 'root' &&
            pg_process_user

    if (!attemptChown && pg_process_user && scriptUser != pg_process_user) {
        warn "We need to have tablespace directories that are owned by " +
                "the user %s. Since we are not running as root, this " +
                "will not be possible. Consequently, the script WILL " +
                "FAIL unless you have previously ensured that the " +
                "directories under %s exist and are owned by %s",
                pg_process_user, transmart_db_tablespace_location,
                pg_process_user
    }
    if (!attemptChown && !pg_process_user) {
        warn "We need to have tablespace directories that are owned by " +
                "the user postgres runs as. But since we were unable to " +
                "determine which user that is, we cannot do that. You will " +
                "still have to ensure that the directories under %s exist " +
                "and are owned by the postgresql user. Otherwise, the script " +
                "WILL FAIL at later stages",
                transmart_db_tablespace_location
        pause()
    }


    mkdir(dir: transmart_db_tablespace_location)
    ['deapp','biomart','transmart','indx','search_app'].each {
        def db_dir = transmart_db_tablespace_location | it;

        mkdir(dir: db_dir);
        if (attemptChown) {
            chown(owner: pg_process_user, type: 'dir', file: db_dir)
        } else if (pg_process_user) {
            try {
                Class.forName('java.nio.file.Files')
                String owner = java.nio.file.Files.getOwner(
                        java.nio.file.Paths.get(db_dir)).name
                if (owner != pg_process_user) {
                    fail "Owner of %s should be %s, but it's %s and we are " +
                            "unable to do anything about it. Aborting",
                            db_dir, pg_process_user, owner
                }
            } catch (ClassNotFoundException cnfe) {
                warn "Cannot detect owner for %s (only possible in Java 7)",
                        db_dir
                warn "ASSUMING THE OWNER IS CORRECT"
            }
        }
    }
}


//======================= UTILITY METHODS ==============================================================

def readCounts(readLines)
{
    def counts = [:]
    readLines.each {
        def matcher = it =~ /^\s*(\w*)\s*\|\s*(\d+)\s*$/
        if (matcher) counts[matcher.group(1)] = matcher.group(2)
    }
    counts
}

def assertEquals(map,key,value)
{
    info "checking: ${key} | ${map[key]} = ${value} ..."
    assert map[key].toString().equals(value.toString())
}

Thread tailThread(File file) {
    def safeSize = {
        def lastSize
        try {
            lastSize = file.size()
        } catch (FileNotFoundException fnf) {
            lastSize = 0
        }
        lastSize
    }

    new Thread({
        def lastSize = safeSize()

        try {
            while (true) {
                Thread.sleep(500)

                def currentSize = safeSize()
                if (currentSize < lastSize) {
                    lastSize = 0
                }
                if (currentSize == lastSize) {
                    continue
                }

                def fis = null
                try {
                    try {
                       fis = new FileInputStream(file)
                    } catch (FileNotFoundException fn) {
                        continue
                    }

                    byte[] buffer = new byte[8192]
                    int left = currentSize - lastSize

                    fis.skip(lastSize)
                    while (left > 0) {
                        def read = fis.read(buffer, 0, Math.min(left, 8192))
                        if (read == -1)
                            break

                        System.out.print(new String(buffer, 0, read, "ISO-8859-1"))
                        left -= read
                    }

                    lastSize = currentSize
                } finally {
                    if (fis)
                        fis.close()
                }
            }
        }  catch (InterruptedException ie) {
            // exit thread
        }
    } as Runnable)
}

def commandStepN(Map args = [:], command)
{
    args = new HashMap(args)

    args.failOnError = args.failOnError == null ? true : args.failOnError
    args.dir         = args.dir ?: '.'
    args.inputString = args.inputString
    args.quiet       = args.quiet == null ? false : args.quiet
    args.timeout     = args.timeout == null ? 600000 : args.timeout
    args.output      = args.output == null ? ant_last_step_output : args.output;
    args.environment = args.environment ?: [:]

    if (!args.callback) {
        commandStep(command, args.failOnError, args.dir, args.inputString,
                args.quiet, args.timeout, args.output, args.environment)
    } else {
        args.callback(args, command)
    }
}

def pause() {
    System.err.println("Press ENTER to continue")
    System.in.read()
    System.in.skip(System.in.available())
}

def commandStep(command, failOnError, dir=".", inputString=null, quiet=false,
                timeout = 600000, output=ant_last_step_output, environment=[:])
{
    // Uncomment this for an interactive mode:
    //println "About to execute $command on dir ${dir ? dir : 'CWD' }..."
    //pause()

    //workaround for quotes in dir...this gives problems, so remove all
    dir = dir.replaceAll("\"", "")

    def outputThread = null
    if (!quiet) {
        outputThread = tailThread(output instanceof File
                                  ? output
                                  : new File(output))
        outputThread.start()
    }

    def ant = new AntBuilder()
    try {
        def executable = command.get(0)
        //remove executable from list:
        command.remove(0)

        info "Executing %s %s", executable, command.collect { "'$it'" }.join(" ")

        ant.exec(dir: dir,
                failonerror: failOnError,
                inputstring: inputString,
                output: output,
                timeout: timeout,
                resultProperty: "cmdExit",
                executable: executable)
                {
                    command.each {
                        arg(value: it)
                    }
                    environment.each { k, v ->
                        env(key: k, value: v)
                    }
                }

    } catch (Exception e) {
        fail "Exception was raised: %s", e.message
    } finally {
        outputThread?.interrupt()
    }

    ant.project.properties.cmdExit == "0" ? true : false
}

def gitStep(repositoryUrl, destDir, branch=null, failOnError=true)
{
    def targetDir = new File(destDir)

    if (!targetDir.exists()) {
        def command = [tools_git_cmd, 'clone', repositoryUrl, destDir]
        //checkout specified branch :
        if (branch)
        {
            if (git_has_single_branch)
                command.add('--single-branch')

            command.add("-b")
            command.add(branch)
        }
        commandStep(command, failOnError, ".", null, false, 1800000) //TODO show progress...perhaps by reading output in a separate thread...?
    }
    else {
        /* maybe we should check whether the repository is dirty/deviated from origin & abort */

        def command = [tools_git_cmd, 'fetch', '-q', 'origin']
        commandStep(command, failOnError, destDir, null, false, 1800000)

        command = [tools_git_cmd, 'rev-parse', '--abbrev-ref', '--symbolic-full-name',  '@{u}']
        def proc = command.execute([], targetDir)
        proc.waitFor()
        def ref = proc.in.text.trim()

        command = [tools_git_cmd, 'reset', '--hard', '-q', ref]
        commandStep(command, failOnError, destDir)
    }
}

def maybeSudoize(Map map = [:], command)
{
    def user = map.user ?: psql_sudo_user
    psql_sudo_user ? ['sudo', '-E', '-u', user, *command] : command
}

def executePsql(Map map = [:], List<String> extraParams, tool = null)
{
    def databaseName = map.database ?: pg_database
    map.environment  = map.environment ?: [:]
    tool             = tool ?: tools_psql_cmd

    params = ['-d', databaseName]
    if (pg_user) {
        params << '-U'
        params << pg_user
    }
    if (pg_host) {
        params << '-h'
        params << pg_host
    }
    if (pg_port) {
        params << '-p'
        params << pg_port
    }
    if (pg_password) {
        map.environment.PGPASSWORD = pg_password
    }

    def command = maybeSudoize([tool, *params, *extraParams])
    commandStepN(map, command)
}

def dbCommand(Map map = [:], sqlCommand)
{
    executePsql(map, ['-c', sqlCommand])
}

def dbScriptStep(scriptFileName, databaseName, failOnError=true, quiet=true, dir = ".", password=null)
{
    info "About to execute SQL script %s", scriptFileName

    def extraParams = ["-f", scriptFileName]

    if (quiet) {
        extraParams << "--quiet"
    }

    executePsql(extraParams, database: databaseName, failOnError: failOnError,
            dir: dir, timeout: 36000000)
}


def replaceInFile(String fileName, String value, String newValue)
{
    def fileAsString = file2String(fileName)
    fileAsString = fileAsString.replaceAll(value, newValue)
    string2File(fileAsString, fileName)
}

/**
 * quick method, handy for SMALL files
 *
 * @return
 * @throws java.io.IOException
 */
public static String file2String(String fileName) throws IOException
{
    InputStream fileInputStream = new FileInputStream(new File(fileName))
    String result = "";
    try
    {
        byte[] buf = new byte[2048];
        int i = 0;
        while((i=fileInputStream.read(buf))!=-1)
        {
            result += new String(buf, 0, i);
        }
    }
    finally
    {
        fileInputStream.close();
    }
    return result;
}

private static String string2File(String value, String fileName) throws IOException
{
    FileWriter outFile = new FileWriter(fileName);

    try
    {
        outFile.write(value)
    }
    finally
    {
        outFile.close();
    }

}

def readProperties(propertiesFileName)
{
    def propertiesFile = new FileReader(propertiesFileName)
    def properties = new Properties()
    properties.load(propertiesFile)
    propertiesFile.close()

    properties
}
/**
 * This method will replace the value of the given property with the given value in the
 * property file.
 *
 * @param propertiesFileName : .property file to change
 * @param propertyName       : the property to change (e.g. "jboss.home" )
 * @param newPropertyValue   : the new value for this property
 * @param putQuotes          : if it is a Groovy properties file, then we should (?) quote string (like file paths)
 */
def replacePropertyInFile(propertiesFileName, propertyName, newPropertyValue, putQuotes = false)
{
    //workaround for quotes in dir...this gives problems, so remove all
    propertiesFileName = propertiesFileName.replaceAll("\"", "")
    if (putQuotes)
        newPropertyValue = "\"" + newPropertyValue + "\""

    def propertiesFileContent = file2String(propertiesFileName)
    propertiesFileContent = propertiesFileContent.replaceAll(propertyName + ".*\\n", propertyName + " = " + newPropertyValue + "\n")
    string2File(propertiesFileContent, propertiesFileName)

    / * the implementation below is more elegant, but it messes up the properties file (both the order of the properties and the comments)
    def properties = readProperties(propertiesFileName)
    properties.setProperty(propertyName, newPropertyValue)
    def output = new FileOutputStream(propertiesFileName)   */
    //the # character added here gives problems in Groovy parser ! properties.store(output, "# nb: " + propertyName + " changed by transmastAppIntaller ")
    //output.close()
}

def which(String executable) {
    def separator = host_os_type == 'windows' ? ';' : ':'
    def paths = System.getenv().PATH.split(separator) /* does not handle quoted paths */

    paths.findResult {
        def fullPath = it | executable | exec_postfix
        new File(fullPath).canExecute() ? fullPath : null
    }
}


def echo(String severity, String format, Object... args) {
    ant.echo message: sprintf(format, args), level: severity
}
def fail(String format, Object... args) {
    ant.fail message: sprintf(format, args)
}

/**
 * Returns the system/user's temporary directory
 *
 * @return
 */
def getTempDir()
{
    System.getProperty("java.io.tmpdir")
}
/* vim: set ts=4 sw=4 tw=120 et filetype=groovy :*/
